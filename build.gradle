import static java.lang.System.getenv
import static org.gradle.api.tasks.testing.logging.TestExceptionFormat.FULL;
import static org.gradle.api.tasks.testing.logging.TestLogEvent.FAILED;

group = "com.leaprnd.deltadom"
version = "1.0.0"

apply plugin: "java-library"

sourceSets {
	test {
		resources {
			srcDir "src/main/javascript"
		}
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation group: "com.google.guava", name: "guava", version: "31.0.1-jre"
	implementation group: "net.sf.trove4j", name: "trove4j", version: "3.0.3"
	testImplementation group: "org.junit.jupiter", name: "junit-jupiter", version: "5.8.2"
	testImplementation group: "net.sf.jtidy", name: "jtidy", version: "r938"
	testImplementation group: "org.mockito", name: "mockito-core", version: "4.2.0"
	testImplementation group: "org.seleniumhq.selenium", name: "selenium-chrome-driver", version: "4.1.1"
	testImplementation group: "org.seleniumhq.selenium", name: "selenium-java", version: "4.1.1"
	testImplementation group: "org.simpleframework", name: "simple-http", version: "6.0.1"
}

test {
	useJUnitPlatform();
	testLogging {
		events FAILED
		exceptionFormat FULL
		showStackTraces true
		showCauses true
	}
}

tasks.withType(Test).configureEach {
	reports.html.required = false
	reports.junitXml.required = false
}

task sourcesJar(type: Jar) {
	archiveClassifier = "sources"
	from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
	archiveClassifier = "javadoc"
	from tasks.javadoc.destinationDir
}

apply plugin: "maven-publish"

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
			artifact sourcesJar
			artifact javadocJar
			pom {
				name = "DeltaDOM"
				description = "A high performance Java library for finding the differences between two HTML documents"
				url = "https://github.com/Leap-R-D/DeltaDOM"
				licenses {
					license {
						name = "Apache-2.0"
						url = "https://opensource.org/licenses/Apache-2.0"
					}
				}
				developers {
					developer {
						name = "Ryan Holdren"
						email = "ryan@holdren.ca"
					}
				}
				scm {
					connection = "scm:git:git://github.com/Leap-R-D/DeltaDOM.git"
					developerConnection = "scm:git:ssh://github.com/Leap-R-D/DeltaDOM.git"
					url = "https://github.com/Leap-R-D/DeltaDOM.git"
				}
			}
		}
	}
	repositories {
		maven {
			name = "OSSRH"
			url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
			credentials {
				username = getenv("MAVEN_CENTRAL_USERNAME")
				password = getenv("MAVEN_CENTRAL_PASSWORD")
			}
		}
	}
}

apply plugin: "signing"

signing {
	sign publishing.publications
}